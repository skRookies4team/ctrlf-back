plugins {
    id 'org.springframework.boot' version '3.3.3' apply false
    id 'io.spring.dependency-management' version '1.1.4' apply false
    id 'java'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.company'
    version = '1.0.0'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
    }

    repositories {
        mavenCentral()
    }

    // Centralize BOMs for all modules (incl. api-gateway)
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.3.3"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.4"
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.32'
        annotationProcessor 'org.projectlombok:lombok:1.18.32'
        testCompileOnly 'org.projectlombok:lombok:1.18.32'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'

        // OpenFeign (공통)
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        testCompileOnly {
            extendsFrom testAnnotationProcessor
        }
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }

    // Windows/CI 환경에서 한글 주석/문자열 컴파일 깨짐 방지
    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    // 콘솔 로그 한글 깨짐 방지 (BootRun 실행 시 JVM 기본 인코딩 고정)
    plugins.withId('org.springframework.boot') {
        tasks.withType(org.springframework.boot.gradle.tasks.run.BootRun).configureEach {
            jvmArgs '-Dfile.encoding=UTF-8'
        }
    }

    // Spotless removed
}
